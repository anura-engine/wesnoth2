{
	id: "unit_avatar",
	is_strict: true,
	always_active: true,

	zorder: 200,

	properties: {
		unit: { type: "class unit" },
		orbs: { type: "[obj orb]", default: [] },
		disc: { type: "null|obj unit_disc" },
		lifebar: { type: "null|obj lifebar" },
		crown: { type: "null|obj crown" },

		hidden_in_fog: {
			type: "bool",
			default: false,
			set: "if(value != _data, [
				set(_data, value),
				map([me,disc,lifebar,crown] + orbs,
					if(value,
						animate(value, {
							alpha: if(context.value, 0, 255),
						}, {
							duration: if(cycle > 5, 10, 0),
						})
					)
				)
			])",
		},

		perspective: {
			type: "string",
			default: "s",
			set: "if(_data != value, [set(_data, value); set_anim(_anim)])",
		},

		_anim: { type: "string", default: "stand" },

		variant: { type: "null|string", default: "", get: "if(_data and (('+' + _data) in available_animations), _data)" },
		_variant_anim: "def(string anim) ->string
			if(variant, '+' + variant + '-' + anim, anim)
		",

		_stand_anim: "string :: if(variant, '+' + variant, 'stand')",

		_anim_finished: { type: "bool", default: false },

		play_anim_blocking: "def(string name) ->commands
			set_anim(name);
			set(_anim_finished, false);
			sleep_until(_anim_finished)
		",

		set_anim: "def(string name) ->commands
		[
			set(_anim, name),
			if(animation != new_anim, set(animation, new_anim)),
		]

		where new_anim =
			if(name = 'stand',
				if(_variant_anim(perspective) in available_animations, _variant_anim(perspective),
				   _stand_anim in available_animations, _stand_anim, _variant_anim('s')),

			   if(_variant_anim(perspective + '-' + name) in available_animations,
				  _variant_anim(perspective + '-' + name),
				  _variant_anim(name) in available_animations, _variant_anim(name),
				  _variant_anim(perspective) in available_animations, _variant_anim(perspective),
				  _stand_anim)
			)
		",

		on_board: { type: "bool", default: true },

		guard_image: { type: "null|obj image_widget" },

		controller: { type: "obj game_controller" },

		damage_flash: { type: "bool", default: false },

		animate_damage: "def(int amount, bool critical) ->commands
		[
			spawn('label', {
				_text: if(amount = 0, ['Miss!'], critical, ['Critical!', str(amount)], [str(amount)]),
				use_absolute_screen_coordinates: false,
				zorder: 10000,
				_color: [1,0,0,1],
				mid_x: mid_x,
				mid_y: mid_y,
				_font_size: 30,
			}, [
				animate(child, {
					mid_y: mid_y-150,
					alpha: 0,
				}, {
					duration: 80,
					on_complete: remove_object(child),
				})
			]),

			set(damage_flash, true),
			schedule(2, set(damage_flash, false)),
			if(lifebar != null,
				animate(lifebar, {
					current_hitpoints: lifebar.current_hitpoints - amount,
				}, {
					duration: 5,
				})
			),
		]
		",

		launch_projectile_animation_blocking: "def(UnitAttack info, int damage) ->commands
			play_anim_blocking(info.attack.animation)
		",
		launch_projectile_delay: "int :: 0",
		launch_projectile_impact_point: "int :: 23",
		melee_impact_point: "int :: 23",

		summon_halo: "null|string :: null",
		summoned_halo: "null|string :: null",

		show_halo: "def(null|string name) ->commands
		if(name != null,
			spawn(name, {
				mid_x: mid_x,
				mid_y: mid_y,
				zorder: zorder+10,
			})
		)
		",

		death_animation_blocking: "def(class animation.unit_death info) ->commands
		[
			map([lifebar,disc,crown] + orbs,
				if(value,
					animate(value, {
						alpha: 0,
					}, {
						duration: 20,
					})
				)
			),

			animate(me, {
				alpha: 0,
			}, {
				duration: 20,
				on_complete: remove_object(me),
				sleep: true,
			}),
		]
		",

		melee_attack_animation_blocking: "def(UnitAttack info, obj unit_avatar target, int damage) ->commands
		[
			play_anim_blocking(info.attack.animation)
		]
		",

		cast_animation_blocking: "def(class animation.unit_cast info) ->commands
		[
			show_halo(summon_halo),
			play_anim_blocking(info.spell.animation),
		]",

		summon_animation_blocking: "def(class animation.unit_summon info) ->commands
		[
			show_halo(summon_halo),
			play_anim_blocking('magic'),
		]",

		summoned_animation: "def(class animation.unit_summon info) ->commands
		;
		[
			set(alpha, 0);
			animate(me, {
				alpha: 255,
			}, {
				duration: 40,
				on_complete: set(me.on_board, true); fire_event(me, 'create'),
			}),
			show_halo(summoned_halo),
		]",


		update: "def() ->commands
		execute(me,
		[
			if(unit.guard and guard_image = null, [
				spawn('image_widget', {
					image: 'guard-icon.png',
					image_area: [0,0,31,31],
					draw_width: 32,
					draw_height: 32,
					zorder: me.zorder+1,
				}, [
					set(me.guard_image, child),
				])
			]),

			if(unit.guard = false and guard_image, [
				remove_object(guard_image),
				set(guard_image, null),
			]),

			if(lifebar, [
				set(lifebar.current_hitpoints, unit.current_hitpoints),
				set(lifebar.maximum_hitpoints, unit.hitpoints),
			]),
		])",

		animate_heal: "def(class animation.unit_heal heal) ->commands
			show_halo(summon_halo);
			if(lifebar != null,
				animate(lifebar, {
					current_hitpoints: lifebar.current_hitpoints + heal.amount,
				}, {
					duration: 5,
				})
			)
		",

		animate_movement: "def(class animation.unit_move move) ->commands execute(me,
		[
			if(size(path) >= 2, [
				set(mid_x, controller.tile_mid_x(path[0])),
				set(mid_y, controller.tile_mid_y(path[0])),

				map(path[1:],
					animate(me, {
						mid_x: controller.tile_mid_x(value),
						mid_y: controller.tile_mid_y(value),
					}, {
						name: 'move',
						duration: 10,
						replace_existing: false,
						on_complete: (; controller.unit_animate_to_loc(me, value)),
						on_process: [
							set(facing, if(controller.tile_mid_x(this_loc) < controller.tile_mid_x(next_loc), 1, -1)),
							set(perspective, if(controller.tile_mid_y(this_loc) < controller.tile_mid_y(next_loc), 's', 'n'));

							set(hidden_in_fog, controller.fog_masks != null and (next_loc not in controller.fog_masks)),
						] where this_loc = path[index]
						  where next_loc = path[index+1],
						
						sleep: true,
					})
				)
			])
		]
		) where path = move.path",

		_preview_pane: { type: "null|obj unit_pane" },
	},

	events: {
		"+end_melee_attack_anim": "set_anim('stand')",
		"+end_melee_attack-s_anim": "set_anim('stand')",
		"+end_melee_attack-n_anim": "set_anim('stand')",
		"+end_n_anim": "set(animation, 'n')",
		"+end_s_anim": "set(animation, 's')",
		"+end_stand_anim": "set(animation, _stand_anim)",
		"+end_magic_anim": "set_anim('stand')",

		end_anim: "set(_anim_finished, true); set_anim('stand')",

		create: "
		set(variant, me.unit.variation);
		[
		if(hidden_in_fog, set(alpha, 0)),
		set_anim(_anim),
		if(on_board, [
			map(range(unit.num_moves),
				spawn('orb', {
					mid_x: mid_x,
					mid_y: mid_y - index*10,
					is_enemy: unit.controller != controller.state.nplayer,
					alpha: if(hidden_in_fog, 0, 255),
				}, [
					add(me.orbs, [child]),
					debug('SPAWN ORB'),
				])
			),

			if(unit.is_ruler = true,
				spawn('crown', {
					mid_x: mid_x,
					y: y,
					alpha: if(hidden_in_fog, 0, 255),
				}, [
					set(me.crown, child),
				])
			),

			spawn('lifebar', {
				current_hitpoints: unit.current_hitpoints,
				maximum_hitpoints: unit.hitpoints,
				alpha: if(hidden_in_fog, 0, 255),
			}, [
				set(me.lifebar, child),
			]),

			if(unit.is_building = false,
				spawn('unit_disc', {
					mid_x: mid_x,
					mid_y: y2,
					zorder: zorder-1,
					alpha: if(hidden_in_fog, 0, 255),
					color_map: if(
						unit.controller = 0, [1.0,0.0,0.0],
						unit.controller = 1, [0.0,0.0,1.0],
						unit.controller = 2, [0.0,1.0,0.0],
						unit.controller = 3, [1.0,1.0,0.0],
						unit.controller = 4, [1.0,0.0,1.0],
						[1.0,1.0,1.0]),
				}, [
					set(me.disc, child),
				])
			),
		]
		)
		]
		",

		process: "
		[
			map(orbs, [
				set(value.is_active, unit.has_moved <= index),
				set(value.is_currently_moving, controller.state.unit_moving = unit and unit.has_moved = index+1),
				set(value.mid_x, me.mid_x - 24),
				set(value.mid_y, me.mid_y - 20 - index*10),
				set(value.zorder, me.zorder+1),
			]),

			if(crown, [
				set(crown.mid_x, me.mid_x - 24),
				set(crown.y, me.mid_y - 46),
				set(crown.zorder, me.zorder+2),
			]),

			if(disc, [
				set(disc.mid_x, me.mid_x),
				set(disc.mid_y, me.y2-14),
				set(disc.zorder, me.zorder-1),
			]),

			if(lifebar, [
				set(lifebar.mid_x, me.mid_x - 25),
				set(lifebar.y, me.mid_y - 14),
				set(lifebar.zorder, me.zorder+1),
			]),

			if(guard_image, [
				set(guard_image.x, x+20),
				set(guard_image.y, y+20),
			])
		]
		",

		mouse_enter: "
			if(on_board and find(level.chars, value is obj unit_pane and value.unit = unit) = null, [
				spawn('unit_pane', {
					unit: unit,
					controller: controller,
					x: if(controller.mouse_pos_abs[0] > SCREEN_WIDTH/2,
						 x - 190,
						 x2 + 10
					   ) - controller.screen_x,
					mid_y: mid_y - controller.screen_y,
					zorder: lib.zorder.gui_pane + 10,
					has_border: true,
				}, [
					set(_preview_pane, child),
				]),
			])
		",

		mouse_leave: "[
			remove_object(_preview_pane),
			set(_preview_pane, null),
		]",

		being_removed: "map(spawned_children, remove_object(value))",
		type_updated: "fire_event('create')",
	},

	shader: {
		name: "unit_shader",
		vertex: "
        uniform mat4 u_anura_mvp_matrix;
        attribute vec4 a_anura_vertex;
        attribute vec2 a_anura_texcoord;
        varying vec2 v_texcoord;

		void main()
		{
            v_texcoord = a_anura_texcoord;
           	gl_Position = u_anura_mvp_matrix * a_anura_vertex;
		}
		",

		fragment: "
		#version 120
		uniform sampler2D u_anura_tex_map;
		uniform float u_alpha;
		uniform bool u_flash;
		varying vec2 v_texcoord;
		void main()
		{
			vec4 col = texture2D(u_anura_tex_map, v_texcoord);
			if(u_flash) {
				col[0] = col[1] = col[2] = 1.0;
			}
			col[3] *= u_alpha;
			gl_FragColor = col;
		}
		",


		create: "[
		]",

		draw: "[
			set(uniform_commands.u_alpha, parent.alpha/255.0),
			set(uniform_commands.u_flash, parent.damage_flash),
		]",
	},
}
