{
	id: "login_controller",
	hidden_in_game: true,
	is_strict: true,

	properties: {
		screen_width: "int :: SCREEN_WIDTH",
		screen_height: "int :: SCREEN_HEIGHT",

		_titlescreen_controller: { type: "obj titlescreen_controller" },

		_removed_widgets: { type: "[custom_obj]", default: [] },

		return: "def() ->commands [
			map(_removed_widgets, add_object(value)),
			map([me] + _widgets, remove_object(value)),
			set(_widgets, []),
		]",

		_enter_login_or_register: "def() ->commands [
			map(_widgets, remove_object(value)),
			set(_widgets, []),

			_add_button('Login', 60*0, me._enter_login_screen),
			_add_button('Register', 60*1, me._enter_register_screen),
			_add_button('Cancel', 60*5, me.return),

		]",

		_enter_login_screen: "def() ->commands [
			map(_widgets, remove_object(value)),
			set(_widgets, []),

			spawn('text_entry', {
				zorder: zorder+1,
				_width: lib.wesnoth.py(160),
				_password: false,
				mid_x: screen_width/2,
				mid_y: screen_height/2 + lib.wesnoth.py(80),
				default_text: 'Username',
			}, [
				add(_widgets, [child]),
				set(_username_entry, child),
			]),

			spawn('text_entry', {
				zorder: zorder+1,
				_width: lib.wesnoth.py(160),
				_password: true,
				mid_x: screen_width/2,
				mid_y: screen_height/2 + lib.wesnoth.py(80 + 40),
				default_text: 'Password',
			}, [
				add(_widgets, [child]),
				set(_passwd_entry, child),
			]),

			spawn('button_controller', {
				text: 'Remember Me',
				zorder: zorder+1,
				mid_x: screen_width/2,
				mid_y: screen_height/2 + lib.wesnoth.py(80 + 40*2),
				_font_size: lib.wesnoth.px(14),
				button_height: lib.wesnoth.py(24),
				checked: remember_me,
				on_click: def() ->commands set(remember_me, not remember_me),
			}, [
				add(_widgets, [child]),
			]),

			if(_error_message != null,
				spawn('label', {
					mid_x: screen_width/2,
					mid_y: screen_height/2 + lib.wesnoth.py(80 + 40*4),
					_color: [1,0,0,1],
					_text: [_error_message],
					zorder: zorder+1,
					_font_size: lib.wesnoth.py(14),
				}, [
					add(_widgets, [child]),
				])
			),

			_add_button('Login', 60*4, me._do_login),
			_add_button('Cancel', 60*5, me._enter_login_or_register),

		]",

		_do_login: "def() ->commands
		execute(me,
		[
			set(_registering, false),
			set(_client, cl),
			debug('SEND LOGIN'),
			execute(me, tbs_send(cl, {
				type: 'login',
				user: get_username(),
				passwd: md5(get_password()),
				remember: remember_me,
			})),
		] where cl = tbs_client(TBS_SERVER_ADDRESS, TBS_SERVER_PORT, { retry_on_timeout: false })
		)
		",

		_registering: { type: "bool", default: false },

		_client: { type: "object|null", default: null },

		get_username: "def() ->string
			_username_entry.text asserting _username_entry != null
		",

		get_password: "def() ->string
			_passwd_entry.text asserting _passwd_entry != null
		",

		_username_entry: { type: "null|obj text_entry" },
		_passwd_entry: { type: "null|obj text_entry" },
		_confirm_passwd_entry: { type: "null|obj text_entry" },

		remember_me: { type: "bool", default: true },
		_error_message: { type: "null|string" },

		_enter_register_screen: "def() ->commands [
			map(_widgets, remove_object(value)),
			set(_widgets, []),

			spawn('text_entry', {
				zorder: zorder+1,
				_width: lib.wesnoth.py(160),
				_password: false,
				mid_x: screen_width/2,
				mid_y: screen_height/2 + lib.wesnoth.py(80),
				default_text: 'Username',
			}, [
				add(_widgets, [child]),
			]),

			spawn('text_entry', {
				zorder: zorder+1,
				_width: lib.wesnoth.py(160),
				_password: true,
				mid_x: screen_width/2,
				mid_y: screen_height/2 + lib.wesnoth.py(80 + 35),
				default_text: 'Password',
			}, [
				add(_widgets, [child]),
			]),

			spawn('text_entry', {
				zorder: zorder+1,
				_width: lib.wesnoth.py(160),
				_password: true,
				mid_x: screen_width/2,
				mid_y: screen_height/2 + lib.wesnoth.py(80 + 70),
				default_text: 'Confirm Password',
			}, [
				add(_widgets, [child]),
			]),

			spawn('text_entry', {
				zorder: zorder+1,
				_width: lib.wesnoth.py(160),
				_password: false,
				mid_x: screen_width/2,
				mid_y: screen_height/2 + lib.wesnoth.py(80 + 105),
				default_text: 'Email (optional)',
			}, [
				add(_widgets, [child]),
			]),

			spawn('button_controller', {
				text: 'Remember Me',
				zorder: zorder+1,
				mid_x: screen_width/2,
				mid_y: screen_height/2 + lib.wesnoth.py(80 + 35*4),
				_font_size: lib.wesnoth.px(14),
				button_height: lib.wesnoth.py(24),
				checked: true,
				on_click: def() ->commands null,
			}, [
				add(_widgets, [child]),
			]),

			_add_button('Register', 60*4, me._enter_login_or_register),
			_add_button('Cancel', 60*5, me._enter_login_or_register),

		]",

		_add_button: "def(string text, int pos, function()->commands on_click) ->commands
			spawn('button_controller', {
				text: text,
				zorder: zorder+1,
				mid_x: screen_width/2,
				mid_y: screen_height/2 + lib.wesnoth.py(80 + pos),
				on_click: on_click,
			}, [
				add(_widgets, [child]),
			])
		",

		_widgets: { type: "[custom_obj]", default: [] },
	},

	events: {
		create: "[
			[[remove_object(b), add(_removed_widgets, [b])] | b <- level.chars, b is obj button_controller],
			_enter_login_or_register(),

		]",

		process: "if(_client, tbs_process(_client))",

		message_received: "
		debug(['MESSAGE', arg.message]);
		switch(message.type,
			'login_fail', [
				set(_error_message, string<- message.message);
				if(_registering, _enter_register_screen(), _enter_login_screen()),
			],
			'message', if(message.message is string, [
				set(_error_message, string<- message.message);
				if(_registering, _enter_register_screen(), _enter_login_screen()),
			]),
			'error', if(message.message is string, [
				set(_error_message, string<- message.message);
				if(_registering, _enter_register_screen(), _enter_login_screen()),
			]),
			'auto_login_fail', [
				_enter_login_screen()
			],
			'login_success', [
			],
			'registration_success', [
			],
		)
		where message = map<- arg.message",
	},
}
