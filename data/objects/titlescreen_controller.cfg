{
	id: "titlescreen_controller",
	prototype: ["title_controller_base"],
	is_strict: true,
	use_absolute_screen_coordinates: true,
	hidden_in_game: true,

	properties: {
		screen_width: "int<- level.dimensions[2]",
		screen_height: "int<- level.dimensions[3]",	

		_play_button: { type: "obj button_controller" },
		_multiplayer_button: { type: "obj button_controller" },
		_editor_button: { type: "obj button_controller" },

		_present_ui: "def() ->commands
		spawn('button_controller', {
			text: 'Play',
			zorder: zorder+1,
			mid_x: int(screen_width/2),
			mid_y: int(screen_height/2) + lib.gui.py(80),
			on_click: me._start_campaign,
			use_absolute_screen_coordinates: true,
		}, [
			set(me._play_button, child)
		]);

		spawn('button_controller', {
			text: 'Multiplayer',
			zorder: zorder+1,
			mid_x: int(screen_width/2),
			mid_y: int(screen_height/2) + lib.gui.py(140),
			on_click: def() ->commands spawn('login_controller', { zorder: zorder+1, _titlescreen_controller: me}),
			use_absolute_screen_coordinates: true,
		}, [
			set(me._multiplayer_button, child)
		]);
	
		spawn('button_controller', {
			text: 'Edit Map',
			zorder: zorder+1,
			mid_x: int(screen_width/2),
			mid_y: int(screen_height/2) + lib.gui.py(200),
			on_click: def() ->commands fadeout_and_exit(add_object(object_playable('editor_controller', 0, 0, 1, {}))),
			use_absolute_screen_coordinates: true,
		}, [
			set(me._editor_button, child)
		]);
		
		spawn('label', {
			zorder: zorder+10,
			mid_x: int(screen_width/2),
			mid_y: int(screen_height/3) + lib.gui.py(80),
			_text: ['Wesnoth: Genesis Demo'],
			use_absolute_screen_coordinates: true,
			_font_size: 80,
		});

		spawn('image_widget', {
			mid_x: int(screen_width/2),
			mid_y: int(screen_height/5),
			zorder: zorder+5,
			image: 'logo_256.png',
			image_area: [0,0,255,255],
			draw_width: 200,
			draw_height: 200,
			use_absolute_screen_coordinates: true,
		})",

		fadeout_and_exit: "def(commands|null do_when_finished) ->commands
		// Make sure we can't trigger this again while the fadeout is happening
		[set(b.on_click, def()->commands null) | b <- level.chars, b is obj button_controller];

		map(level.chars,
			animate(value, {
				alpha: 0,
			}, {
				duration: 10,
				on_complete: def()->commands [do_when_finished, remove_object(value)],
			})
		)",

		_start_campaign: "def() ->commands
		execute(me, [
			let msg = {
				type: 'create_game',
				game_type: 'wesnoth2',
				users: [{
					user: 'human',
					session_id: 1,
					ruler: 'Sorceress',
					spellbook: {
						spells: ['Church of Valia', 'Elvish Archer', 'Elvish Archer', 'Elvish Archer', 'Elvish Fighter', 'Elvish Fighter', 'Elvish Fighter', 'Lesser Heal', 'Lesser Heal', 'Energy Bolt', 'Energy Bolt', 'Refresh'],
					},
				}, {
					user: 'ai',
					session_id: 2,
					bot: true,
					bot_type: 'default',
					ruler: 'Orcish Warlord',
					spellbook: {
						spells: ['Wolf Rider', 'Orcish Archer', 'Orcish Assassin']*3,
					},
				}],
			};
		
			//make buttons non-clickable
			[set(b.on_click, def()->commands null) | b <- level.chars, b is obj button_controller];

			let client = tbs_internal_client();
			let request = tbs_blocking_request(client, msg);

			[
				request.block(),
				map(level.chars,
					animate(value, {
						alpha: 0,
					}, {
						duration: 10,
						sleep: true,
					})
				),
			];

			if((not request.response is map) or (map<-request.response).type != 'game_created', [
				//TODO: Creating a local game should rarely fail
				//but if it does we should have better error handling here.
				debug(['ERROR CREATING GAME', request.response]),
				cancel(),
			]);

			[
				map(level.chars, remove_object(value)),
				add_object(controller),
			]

			where controller = object_playable('game_controller', 0, 0, 0, {
				_mm_client_state: _mm_client_state,
				session_id: 1,
				return_from_game: return_from_game,
			})
		])
		",
	},

	events: {
		create: "[
			set(_mm_client_state, construct('mm_client_state', {
				username: '',
				account_info: {
					chat_channels: {},
				},
				account_info_version: 1,
				session_id: -1,
			})),

			set(level.dimensions, [0,0,screen_width,screen_height]),

			// The x,y position and level dimensions need to be set manually her

			set_mouse_cursor('normal')

		] where screen_width = SCREEN_WIDTH
		  where screen_height = SCREEN_HEIGHT",

		window_resize: "[
			set(level.dimensions, [0,0,new_width,new_height]),

			map(level.chars, remove_object(value));

			_present_ui()
		] where new_width = max(int<- arg.width,800)-1
		  where new_height = max(int<- arg.height, 600)-1",
	}
}
