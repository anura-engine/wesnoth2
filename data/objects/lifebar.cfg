{
	id: "lifebar",

	is_strict: true,
	always_active: true,

	properties: {
		current_hitpoints: { type: "decimal", set: "if(value != _data, [set(_data, value); render()])" },
		maximum_hitpoints: { type: "decimal" },
		hp_ratio: "decimal :: decimal(current_hitpoints)/decimal(maximum_hitpoints)",

		_width: 4,
		_height: "int :: 2 + int(maximum_hitpoints)",

		render: "def() ->commands
		set(animation, lib.util.render_frame(c, ww, hh,
		[
			c.rectangle(0,0,ww,hh),
			c.set_source_rgba(0,0,0,1),
			c.fill(),

			c.rectangle(1, hh - hp_ratio*hh, ww-2, hp_ratio*hh),
			if(hp_ratio >= 2.0/3.0,
				c.set_source_rgba(0,1,0,1),
			   hp_ratio >= 1.0/3.0,
				c.set_source_rgba(1,1,0,1),
				c.set_source_rgba(1,0,0,1)
			),
			c.fill(),

			c.rectangle(0,0,ww,hh),
			c.set_source_rgba(1,1,1,1),
			c.set_line_width(2),
			c.stroke(),
		]))
		where hp_ratio = current_hitpoints/decimal(maximum_hitpoints)
		where ww = _width
		where hh = _height
		where c = canvas()
		",
	},

	on_create: "render()",
	on_type_updated: "render()",
}
