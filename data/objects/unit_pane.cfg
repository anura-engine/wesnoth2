{
	id: "unit_pane",
	is_strict: true,
	use_absolute_screen_coordinates: true,

	properties: {
		unit: { type: "class unit" },
		controller: { type: "obj game_controller" },

		_width: { type: "int" },
		_height: { type: "int" },

		render: "def() ->commands
		set(animation, lib.util.render_frame(c, ww, hh,
		[
			c.rectangle(0,0,ww,hh),
			c.set_source_rgba(0.2,0.2,0.2,1.0),
			c.fill(),

			c.set_source_rgba(1,1,1,1),

			map(fragments, [
				c.save(),
				c.translate(50, 12),
				value.path,
				c.fill(),
				c.restore(),
			])

			where fragments = c.markup_text(
			q(<b>) + unit.name + q(</b>\n) +
			str(unit.current_hitpoints) + '/' + str(unit.hitpoints) + ' HP\n' +
			str(unit.move) + ' Move', { width: 200, scale: 1.0 })
		]
		))
		where ww = _width
		where hh = _height
		where c = canvas()
		",

		_create_avatar: "def() ->commands [
			remove_object(_avatar),
			spawn('unit_avatar_' + unit.id, {
				zorder: zorder+1,
				on_board: false,
				x: x - 10,
				y: y,
				unit: unit,
				controller: controller,
				use_absolute_screen_coordinates: true,
			}, [
				set(_avatar, obj unit_avatar<- child),
			]),

			map(unit.attacks,
				spawn('attack_display', {
					mid_x: mid_x,
					y: y + 140 + index*70,
					zorder: zorder+10,
					attack: value,
				}, [
					add(_attacks, [child]),
				])
			),
		]
		",

		_avatar: { type: "null|obj unit_avatar" },

		_attacks: { type: "[obj attack_display]", default: [] },
	},

	on_being_removed: "[
		remove_object(_avatar),
		map(_attacks, remove_object(value)),
	]",

	on_create: "render(); _create_avatar()",

	on_type_updated: "fire_event('create')",
}
