{
	id: "orb",
	is_strict: true,
	always_active: true,

	properties: {
		unit_avatar: { type: "obj unit_avatar" },

		red_map: { type: "[decimal,decimal,decimal]", default: [1.0,0.0,0.0], set: "[set(_data, value), set(shader.uniform_commands.u_red_map, value)] asserting shader" },
		green_map: { type: "[decimal,decimal,decimal]", default: [0.0,1.0,0.0], set: "[set(_data, value), set(shader.uniform_commands.u_green_map, value)] asserting shader" },
		blue_map: { type: "[decimal,decimal,decimal]", default: [0.0,0.0,1.0], set: "[set(_data, value), set(shader.uniform_commands.u_blue_map, value)] asserting shader" },

		is_enemy: { type: "bool", default: false, set: "if(_data != value, [set(_data, value); _calculate_colors()])" },
		is_active: { type: "bool", default: false, set: "if(_data != value, [set(_data, value); _calculate_colors()])" },
		is_currently_moving: { type: "bool", default: false, set: "if(_data != value, [set(_data, value); _calculate_colors()])" },

		_calculate_colors: "def() ->commands [
			if(is_currently_moving, [
				set(red_map, [1.0,0.0,0.0]),
				set(green_map, [1.0,0.0,0.0]),
				set(blue_map, [0.0,0.0,0.0]),
			], is_active, [
				set(red_map, [0.0,0.0,0.0]),
				set(green_map, [1.0*ratio,0.0,0.0]),
				set(blue_map, [0.0,0.0,0.0]),
			], [
				set(red_map, [1.0*ratio,0.0,0.0]),
				set(green_map, [0.0,1.0*ratio,0.0]),
				set(blue_map, [0.0,0.0,0.5*ratio]),
			]) where ratio = 1.0
		]",
	},

	on_create: "_calculate_colors()",

	animation: [{
		id: "normal",
		image: "gui/orb.png",
		rect: [0,0,19,19],
		scale: 0.5,
	}],

	shader: {
		name: "orb_shader",
		vertex: "
        uniform mat4 u_anura_mvp_matrix;
        attribute vec4 a_anura_vertex;
        attribute vec2 a_anura_texcoord;
        varying vec2 v_texcoord;

		void main()
		{
            v_texcoord = a_anura_texcoord;

           	gl_Position = u_anura_mvp_matrix * a_anura_vertex;
		}
		",

		fragment: "
		#version 120
		uniform sampler2D u_anura_tex_map;
		uniform vec3 u_red_map;
		uniform vec3 u_green_map;
		uniform vec3 u_blue_map;
		uniform float u_cycle;
		uniform bool u_active;
		varying vec2 v_texcoord;
		void main()
		{
			vec4 col = texture2D(u_anura_tex_map, v_texcoord);

			gl_FragColor[0] = u_red_map[0]*col[0] + u_red_map[1]*col[1] + u_red_map[2]*col[2];
			gl_FragColor[1] = u_green_map[0]*col[0] + u_green_map[1]*col[1] + u_green_map[2]*col[2];
			gl_FragColor[2] = u_blue_map[0]*col[0] + u_blue_map[1]*col[1] + u_blue_map[2]*col[2];
			gl_FragColor[3] = col[3]*0.75;
		}
		",


		create: "[
			set(uniform_commands.u_red_map,   [1.0,0.0,0.0]),
			set(uniform_commands.u_green_map, [0.0,1.0,0.0]),
			set(uniform_commands.u_blue_map,  [0.0,0.0,1.0]),
		]",

		draw: "[
			set(uniform_commands.u_cycle, parent.cycle),
			set(uniform_commands.u_active, parent.is_active),
		]",

	},
}
