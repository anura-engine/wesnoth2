{
	id: "attack_display",
	prototype: ["action_display"],

	properties: {

		attack: { type: "class attack" },
		action: "class action :: attack",

		render: "def() ->commands
		set(animation, lib.util.render_frame(c, ww, hh,
		[
			c.rectangle(0,0,ww,hh),
			if(selected,
				c.set_source_rgba(0.6,0.3,0.3,1.0),
				has_mouseover,
				c.set_source_rgba(0.5,0.4,0.4,1.0),
				c.set_source_rgba(0.3,0.3,0.3,1.0)
			),
			c.fill(),

			c.rectangle(0,0,ww,hh),
			c.set_source_rgba(0.4,0.4,0.4,1.0),
			c.set_line_width(2),
			c.stroke(),

			c.set_source_rgba(1,1,1,1),

			map(fragments, [
				c.save(),
				c.translate(66, 4),
				value.path,
				c.fill(),
				c.restore(),
			]),
		]
		where fragments = c.markup_text(
			q(<b>) + attack.name + q(</b>\n) +
			str(attack.damage) + q(x) + str(attack.strikes) + '\n' +
			if(attack.mana_cost, str(attack.mana_cost) + ' mana\n', '') +
			if(attack.ranged, 'Ranged\n', '') +
			fold(map(attack.abilities, value + '\n'), a+b, ''),
			{ width: 200, scale: 0.8 }
		)
		))
		where ww = _width
		where hh = _height
		where c = canvas()
		",

		_spawn_image: "def() ->commands [
			remove_object(_image),
			spawn('image_widget', {
				x: x+4,
				y: y+4,
				zorder: zorder+10,
				image: 'attacks/' + attack.icon,
				image_area: [0,0,59,59],
				draw_width: 60,
				draw_height: 60,
				use_absolute_screen_coordinates: true,
			}, [
				set(_image, child),
			]),
		]
		",

		_image: { type: "null|obj image_widget" },
	},

	events: {
		being_removed: "remove_object(_image)",

		create: "render(); _spawn_image()",
	}
}
